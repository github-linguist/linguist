def forall( l : [Bool] ) -> Bool {
  fold acc = true, x <- l do
    (acc and x)
  end
}

def boolean_list_eq( l1 : [Bool], l2 : [Bool] ) -> Bool {

  def boolean_list_compare( l1 : [Bool], l2 : [Bool] ) -> [Bool] {
    for x1 <- l1, x2 <- l2 do
      ( ( x1 and x2 ) or not( x1 or x2 ) ) : Bool
    end
  }
  
  let cmp : [Bool] = boolean_list_compare( l1 = l1, l2 = l2 );
  forall( l = cmp )
}


def string_list_eq( l1 : [Str], l2 : [Str] ) -> Bool {

  def string_list_compare( l1 : [Str], l2 : [Str] ) -> [Bool] {
    for x1 <- l1, x2 <- l2 do
      ( x1 == x2 ) : Bool
    end
  }
  
  let cmp : [Bool] = string_list_compare( l1 = l1, l2 = l2 );
  forall( l = cmp )
}






 def output_bool() -> <out1 : Bool, out2 : Bool>
in Racket *{
  (define out1 #t)
  (define out2 #f)
}*

let <out1 = c1 : Bool, out2 = d1 : Bool> =
  output_bool();

let output_bool_test : Str =
  if( not c1 or d1 )
  then
    error "output_bool" : Str
  else
    "ok"
  end;




def input_bool( a : Bool, b : Bool ) -> <out : Str>
in Racket *{
  (define out
    (if (and a (not b)) "ok" (error "bad input binding")))
}*

let <out = input_bool_test : Str> =
  input_bool( a = true, b = false );





def output_string() -> <out : Str>
in Racket *{
  (define out "blub")
}*

let <out = c2 : Str> = output_string();

let output_string_test : Str =
  if( c2 == "blub" )
  then
    "ok"
  else
    error "output_string" : Str
  end;







def input_string( a : Str ) -> <out : Str>
in Racket *{
  (define out (if (equal? a "blub") "ok" (error "bad input binding")))
}*

let <out = input_string_test : Str> =
  input_string( a = "blub" );



def output_boolean_list() -> <out : [Bool]>
in Racket *{
  (define out '(#t #f #t))
}*

let <out = c3 : [Bool]> = output_boolean_list();

let d3 : [Bool] = [true, false, true : Bool];

let output_boolean_list_test : Str =
  if boolean_list_eq( l1 = c3, l2 = d3 )
  then
    "ok"
  else
    error "output_boolean_list" : Str
  end;





def output_string_list() -> <out : [Str]>
in Racket *{
  (define out '("a" "b" "c"))
}*

let <out = c4 : [Str]> = output_string_list();

let d4 : [Str] = ["a", "b", "c" : Str];

let output_string_list_test : Str =
  if string_list_eq( l1 = c4, l2 = d4 )
  then
    "ok"
  else
    error "output_string_list" : Str
  end;




def write_file( inp : Str ) -> <f : File>
in Racket *{
  (define f "out.txt")
  (call-with-output-file
    f
    (lambda (output-port) (writeln inp output-port)))
}*

def read_file( f : File ) -> <out : Str>
in Racket *{
  (define out
    (call-with-input-file
      f
      (lambda (input-port) (read-line input-port))))
}*

let c5 : Str = "blub";

let <out = d5 : Str> =
  read_file( f = ( write_file( inp = c5 )|f ) );

let file_test : Str =
  if( c5 == d5 )
  then
    "ok"
  else
    error "file_test" : Str
  end;




[output_bool_test,
 input_bool_test,
 output_string_test,
 input_string_test,
 output_boolean_list_test,
 output_string_list_test,
 file_test : Str];


