%%====================================================================
%% Functions
%%====================================================================


def range( first : Str, last : Str ) ->
      <number_lst : [Str]>

in Matlab *{
  a = str2num( first );
  b = str2num( last );
  m = a:b;
  c = num2cell( m );
  number_lst = cellfun( @num2str, c, 'UniformOutput', false );
}*


def is_multiple( number : Str, divisor : Str ) ->
      <is_multiple : Bool>

in Matlab *{
  a = str2num( number );
  b = str2num( divisor );
  is_multiple = mod( a, b ) == 0;
}*


%%====================================================================
%% Constants
%%====================================================================


let last : Str = 100;


%%====================================================================
%% Workflow
%%====================================================================


let <number_lst = number_lst : [Str]> =
  range( first = 1,
         last  = last );


let fizzbuzz_lst : [Str] =
  for x <- number_lst do

    let <is_multiple = f : Bool> =
      is_multiple( number  = x,
                   divisor = 3 );

    let <is_multiple = b : Bool> =
      is_multiple( number  = x,
                   divisor = 5 );

    if ( f and b ) then "FizzBuzz"
    else
      if f then "Fizz"
      else
        if b then "Buzz"
        else
          x
        end      
      end
    end

    : Str

  end;


%%====================================================================
%% Query
%%====================================================================

fizzbuzz_lst;

